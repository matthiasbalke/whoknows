buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:${version_spotless}"
    }
}

subprojects {

    if (file('src/main/groovy').exists()) {
        apply plugin: 'jacoco'
        apply plugin: 'groovy'
        apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

        repositories {
            jcenter()
        }

        spotless {
            encoding 'UTF-8'

            format 'groovy', {
                target '**/*.groovy'

                indentWithSpaces()
                trimTrailingWhitespace()
                endWithNewline()
            }

            format 'properties', {
                target '**/*.properties'

                trimTrailingWhitespace()
                endWithNewline()

                encoding 'ISO-8859-1'
            }

            format 'sql', {
                target '**/*.sql'

                trimTrailingWhitespace()
                endWithNewline()
            }
        }

        jacoco {
            toolVersion = version_jacoco
        }

        jacocoTestReport {
            reports {
                xml.enabled false
                csv.enabled false
                html.destination "${buildDir}/jacoco/html"
            }
        }

        build.dependsOn jacocoTestReport

        test {
            // set heap size for the test JVM(s)
            minHeapSize = "128m"
            maxHeapSize = "1024m"

            jacoco {
                append = false
                includes = [
                        "com/github/**/*"
                ]
            }
        }

    } else {
        apply plugin: 'base'
    }

}
